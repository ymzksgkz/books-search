import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
	id 'org.springframework.boot' version '3.2.4'
	id 'io.spring.dependency-management' version '1.1.4'
	id 'org.jetbrains.kotlin.jvm' version '1.9.23'
	id 'org.jetbrains.kotlin.plugin.spring' version '1.9.23'
	id 'org.flywaydb.flyway' version '9.22.3'
	id 'org.jooq.jooq-codegen-gradle' version '3.19.7'
	id 'java'
}

group = 'jp.webengineer.books'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '17'
}

repositories {
	mavenCentral()
	maven { url 'https://jitpack.io' }
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-jooq'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'
	implementation 'org.flywaydb:flyway-core'
	implementation 'org.jetbrains.kotlin:kotlin-reflect'
	implementation 'org.jooq:jooq:3.19.7'
	implementation 'com.github.guepardoapps:kulid:2.0.0.0'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	developmentOnly 'org.springframework.boot:spring-boot-docker-compose'
	runtimeOnly 'org.postgresql:postgresql'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.testcontainers:junit-jupiter'
	testImplementation 'org.testcontainers:postgresql'
	jooqCodegen 'org.postgresql:postgresql'
}

tasks.withType(KotlinCompile) {
	kotlinOptions {
		freeCompilerArgs += '-Xjsr305=strict'
		jvmTarget = '17'
	}
}

tasks.named('test') {
	useJUnitPlatform()
}

flyway {
	url = 'jdbc:postgresql://localhost:5432/mydatabase'
	user = 'myuser'
	password = 'secret'
}

jooq {
	configuration {
		jdbc {
			driver = 'org.postgresql.Driver'
			url = 'jdbc:postgresql://localhost:5432/mydatabase'
			user = 'myuser'
			password = 'secret'
		}
		generator {
			name = 'org.jooq.codegen.KotlinGenerator'
			database {
				name = 'org.jooq.meta.postgres.PostgresDatabase'
				inputSchema = 'public'
				excludes = 'flyway_schema_history'

			}
			generate {
				deprecated = false
			}
			target {
				packageName = 'jp.webengineer.bookssearch.jooq'
				directory = "$buildDir/generated"
			}
		}
	}
}

sourceSets {
	main {
		java {
			srcDirs 'src/main/kotlin', "$buildDir/generated"
		}
	}
}